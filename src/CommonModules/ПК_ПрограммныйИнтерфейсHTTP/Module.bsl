
#Область СлужебныйПрограммныйИнтерфейс

Функция ИсполнитьЗапрос(Запрос, НаименованиеМодуля = Неопределено) Экспорт
	ПараметрыЗапросаHTTP = ИнициализироватьПараметрыЗапросаHTTP(Запрос);
	
	Если НаименованиеМодуля <> Неопределено Тогда
		ПараметрыЗапросаHTTP.НаименованиеМодуля = НаименованиеМодуля;
	КонецЕсли;
	
	Попытка
		ПараметрыОтвета = ПК_ИнтеграцияПрограммныйИнтерфейс.СформироватьСтруктуруОтвета(ПараметрыЗапросаHTTP);
		ПараметрыЗапросаHTTP.Вставить("Ответ", СтруктураОтвета(ПараметрыОтвета));
	Исключение
		ПК_ИнтеграцияПрограммныйИнтерфейс.ЗарегистрироватьОшибку(ОписаниеОшибки(), ,
			"Ошибка выполнения запроса");
		ПараметрыЗапросаHTTP.Вставить("Ответ", ПК_ОшибкиОбменаСервер.ОшибкаВыполненияЗапроса());
	КонецПопытки;
	Возврат ПараметрыЗапросаHTTP.Ответ;

КонецФункции // ИсполнитьЗапрос()

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция ИнициализироватьПараметрыЗапросаHTTP(Запрос) Экспорт
	ПараметрыЗапросаHTTP = Новый Структура;
	ПараметрыЗапросаHTTP.Вставить("ИдентификаторЗапроса", Строка(Новый УникальныйИдентификатор));
	ПараметрыЗапросаHTTP.Вставить("ДатаЗапроса", ТекущаяДата());
	ПараметрыЗапросаHTTP.Вставить("HTTPМетод", Запрос.HTTPМетод);
	ПараметрыЗапросаHTTP.Вставить("БазовыйURL", Запрос.БазовыйURL);
	ПараметрыЗапросаHTTP.Вставить("ОтносительныйURL", Запрос.ОтносительныйURL);
	ПараметрыЗапросаHTTP.Вставить("ТелоЗапроса", Запрос.ПолучитьТелоКакСтроку());
	ПараметрыЗапросаHTTP.Вставить("ПараметрыURL", Запрос.ПараметрыURL);
	ПараметрыЗапросаHTTP.Вставить("НаименованиеМодуля", Неопределено);
	
	Если Не Запрос.HTTPМетод = "GET" И Не Запрос.HTTPМетод = "DELETE" Тогда
		ТелоЗапроса = РаскодироватьИзBase64(ПараметрыЗапросаHTTP.ТелоЗапроса);
		ДесериализованноеТелоЗапроса = ДесериализацияJSON(ТелоЗапроса);
		Если ДесериализованноеТелоЗапроса = Неопределено Тогда
			Возврат Неопределено;	
		КонецЕсли;
		ПараметрыЗапросаHTTP.Вставить("ДесериализованноеТелоЗапроса", ДесериализованноеТелоЗапроса);		
	КонецЕсли;
	
	ПараметрыЗапросаURI = Новый Соответствие;
	Для Каждого КлючИЗначение Из Запрос.ПараметрыЗапроса Цикл
		ПараметрыЗапросаURI.Вставить(НРег(КлючИЗначение.Ключ), КлючИЗначение.Значение);
	КонецЦикла;
	ПараметрыЗапросаHTTP.Вставить("ПараметрыЗапросаURI", ПараметрыЗапросаURI);
	
	УРИ = Запрос.БазовыйURL + Запрос.ОтносительныйURL;

	ПараметрыЗапросаСтрокой = "";
	Для Каждого КлючИЗначение Из Запрос.ПараметрыЗапроса Цикл

		Если ЗначениеЗаполнено(ПараметрыЗапросаСтрокой) Тогда

			Если ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
				ПараметрыЗапросаСтрокой = ПараметрыЗапросаСтрокой + "&" + КлючИЗначение.Ключ + "="
					+ КлючИЗначение.Значение;
			Иначе
				ПараметрыЗапросаСтрокой = ПараметрыЗапросаСтрокой + "&" + КлючИЗначение.Ключ;
			КонецЕсли;

		Иначе

			Если ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
				ПараметрыЗапросаСтрокой = "?" + КлючИЗначение.Ключ + "=" + КлючИЗначение.Значение;
			Иначе
				ПараметрыЗапросаСтрокой = "?" + КлючИЗначение.Ключ;
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

	УРИ = УРИ + ПараметрыЗапросаСтрокой;
	ПараметрыЗапросаHTTP.Вставить("УРИ", УРИ);
	
	Возврат ПараметрыЗапросаHTTP;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РаскодироватьИзBase64(СтрокаBase64)

	СтрокаРаскодированная = "";
	Попытка
		СтрокаРаскодированная = ПолучитьСтрокуИзДвоичныхДанных(Base64Значение(СтрЗаменить(СтрокаBase64, """", "")));
	Исключение

	КонецПопытки;
	Если Не ЗначениеЗаполнено(СтрокаРаскодированная) И ТипЗнч(СтрокаBase64) = Тип("Строка") Тогда
		СтрокаРаскодированная = СтрокаBase64;
	КонецЕсли;
	Возврат СтрокаРаскодированная;

КонецФункции

Функция ДесериализацияJSON(ТелоЗапроса) Экспорт
	
	Если Не ЗначениеЗаполнено(ТелоЗапроса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТелоЗапроса);
	
	ДесериализованноеТелоЗапроса = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	Возврат ДесериализованноеТелоЗапроса;
КонецФункции // ДесериализацияJSON()

Функция СтруктураОтвета(СтрокаОтвета, КодОтвета = Неопределено) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, СтрокаОтвета);
	СтрокаJSON = ЗаписьJSON.Закрыть();
	
	Если КодОтвета = Неопределено Тогда
		КодОтвета = ?(ЗначениеЗаполнено(СтрокаОтвета), 200, 204);
	КонецЕсли;
	
	Возврат Новый Структура("СтрокаОтвета, КодОтвета", СтрокаJSON, КодОтвета);
КонецФункции

Функция ОтветJSON(Ответ, HTTPСервисОтвет) Экспорт
	СтрокаJSON = Новый ЗаписьJSON;
	СтрокаJSON.УстановитьСтроку();  
	ЗаписатьJSON(СтрокаJSON, Ответ);
	
	HTTPСервисОтвет.Заголовки.Вставить("Content-type", "application/json");
	HTTPСервисОтвет.УстановитьТелоИзСтроки(СтрокаJSON.Закрыть());
	Возврат HTTPСервисОтвет;
КонецФункции

#КонецОбласти


